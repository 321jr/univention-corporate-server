#!/usr/bin/perl
# 	$Id: dump_wrapper,v 1.2 2003/09/08 07:14:37 thorsten Exp $	
use strict;
use Expect;

if($ARGV[0] =~ /^-/) {	# make the command name the first arg
  unshift(@ARGV, "dump");
}
e2fsdump(@ARGV);


sub e2fsdump {
  my $cmd  = shift;
  my @args = @_;
  
  open(P, "$cmd @args 2>&1 |") || die "cannot spawn process: $!";
  my $exp = Expect->exp_init(\*P);

  $exp->debug(0);
  $exp->exp_internal(0);
  $exp->log_user(1);
  
  my ($i, $ioerr, $rc) = (0, 0, 1);
  for(;;) {			# loop forever
  

    if(defined $exp->exp_exitstatus) {	# this is not very likely
      exit $exp->exp_exitstatus;
    }
    
    if($i > 99) {		# give up in case of more than 100 prompts 
      $exp->soft_close();
    }
    
    if($i > 100) {		# oups? still alive? 
      $exp->hard_close();
    }

    my ($pos, $err, $string) = 
      $exp->expect
	(undef,
	 "Do you want to abort dump?",                # (1) # interupt received
	 "Do you want to rewrite this volume?",       # (2)
	 "Is the new volume mounted and ready to go?",# (3)
	 "Do you want to abort?",                     # (4)
	 "Do you want to retry the open?",            # (5)
	 "Do you want to attempt to continue?",       # (6)
	 "-re", "\"Yes\" or \"No\"\?\r?\n",           # (7)
	 "-re", "DUMP IS DONE\r?\n",                  # (8)
	 "-re", "\r?\n");                             # (9)

#    warn "pos=$pos,  err=$err,  string=$string";

    if($err) {
      for($err) {
	$rc = $exp->exp_exitstatus if defined $exp->exp_exitstatus;
#	warn "rc=$rc";
	/^1:/ and die "unexpected errorcode from expect: $'";
	/^2:/ and exit($rc);
	/^3:/ and exit($rc);
	/^4:/ and die "spawned process sent error: $'\n";
      }
    }

    SWITCH: {
      $pos == 1 and $exp->send("y\r"), $i++, last;
      $pos == 2 and $exp->send("n\r"), $i++, last;
      $pos == 3 and $exp->send("y\r"), $i++, last; 
      $pos == 4 and $exp->send("y\r"), $i++, last;
      $pos == 5 and 
	$ioerr++, $ioerr<10 ? $exp->send("y\r") : $exp->send("n\r"), 
	$i++, last;
      $pos == 6 and $exp->send("n\r"), $i++, last;
      $pos == 7 
	and $exp->send("n\r"), $i++, last; # what was the question???
      $pos == 8 and $rc = 0, last;
      $pos == 9 and last;
    }

  }
}
  

